#ifdef PYTHON_SETUP

T = 298
p = 1
f_h = wf = spec.WATER_FREQUENCY
csw = fcalc.cs_water_Tp(T, p)
scale = 600.0/wf

ut.putcomment('DEFINE EXPERIMENT TYPE')
pp.exp = ExpType(2, ['H','N','C'])

filename = write_bruker_search_path('pp', 'water_nh.jz', sourcefile = 'pp/user/water_nh.jz')
PUTPARS('PULPROG', filename)

ut.putcomment('SETUP PROTON')
PUTPARS('SFO1', wf)

p1lp = spec.get_pulse_power ('H1', 'rect', 7.0, nearby = True)
PUTPARS(('p1', 'pldb1'), p1lp)

p2lp = spec.get_pulse_power ('H1', 'rect', 1000.0*scale, nearby = True)
PUTPARS (('p2', 'pldb2'), p2lp)

p3lp = spec.get_pulse_power ('H1', 'sinc1.0', 2100.0*scale, nearby = True)
filename = write_bruker_search_path('shape', 'sinc1.0', sourcefile = 'wave/user/sinc1.0')  # what is a half gauss sp1? acqu says its a sinc1
PUTPARS(('p3', 'spdb1', 'spnam1', 'spoffs1'), p3lp + (filename, 0))

ut.putcomment('SETUP CARBON')
f_cacb = fcalc.p2f_water(46, 'C13', wf, csw)
f_ca = fcalc.p2f_water(56, 'C13', wf, csw)
f_c = fcalc.p2f_water(177, 'C13', wf, csw)
PUTPARS('SFO3', f_c)

flist = [f_ca, f_c]
flist_string = ut.flist_to_Bruker_flist( flist )
filename = write_bruker_search_path('f1', 'water_nh.jz', sourcetext = flist_string)
PUTPARS('FQ4LIST', filename)

p5lp = spec.get_pulse_power('C13', 'rect', 53*scale)
PUTPARS(('p5', 'pldb5'), p5lp)

ut.putcomment(' SETUP NITROGEN')
f_n = fcalc.p2f_water(116.5, 'N15', wf, csw)
PUTPARS('SFO2', f_n)

p7lp = spec.get_pulse_power ('N15', 'rect', 40, nearby=True)
PUTPARS(('p7', 'pldb7'), p7lp)

p31lp =spec.get_pulse_power('N15', 'rect', 200*scale, nearby = True)
filename = write_bruker_search_path('cpd', 'waltz16.31', sourcefile = 'cpd/user/waltz16.31')
PUTPARS(('p31','pldb31','CPDPRG2'), p31lp + (filename,))

ut.putcomment('SETUP GRADIENTS')
filename = write_bruker_search_path('gp', 'SINE.20', sourcefile = 'gp/SINE.20')
PUTPARS(('GPNAM0', 'GPX0','GPY0','GPZ0'), (filename, 50, 0, 20))

filename = write_bruker_search_path('gp', 'SINE.20', sourcefile = 'gp/SINE.20')
PUTPARS(('GPNAM1', 'GPX1','GPY1','GPZ1'), (filename, 0, 0, 50))

PUTPARS(('p10', 'p11', 'p12'), (3000, 2000, 400))

ut.putcomment('SETUP AQUISITION')
PUTPARS(('in0', 'l3', '1 td', '1 si'), (300e-6, 200, 400, 512))
PUTPARS(('dw', 'td','si'), (54e-6*scale, 1024, 2048))
#endif

#include "bits.sg"

;1 hn, 15n edited noesy to water protons
;history
;written by sg 2/23/93
;put in water flip_back 6/1/93
;change to waternh 7/29/93

;#define ONE_D
#define CARBON_LABEL
;#define PULSE_CHECK
;#define SHORT_T2
;#define N15T2
;#define N15T1RHO

;p1	proton 90 at pl1, 9u
;p2	1ms proton 90  at pl2 ;sklenar
;set phcor14 and phcor18!!!
;p3	~2ms proton 90 with halfgauss sp1

;"p5=53u"		;carbon pulse at pl5

;p7	high power n15 90 pl7 on f2
;p31	low power n15 90 (160ms) on f2 at pl31

"acqt0=0"
;"in0=inf1/2"

#ifndef PULSE_CHECK
#ifdef CARBON_LABEL
"d0=in0*0.5 - p7*0.637 -4u -p5*2.0"	;nitrogen incremental delay
#else
"d0=in0*0.5 - p7*0.637 -p1"
#endif
#else
"d0=5u"
#endif

#ifdef SHORT_T2
"d4=1.5m"			;hsq h to n15
"d5=800u"
#else
"d4=2.25m"			;hsq h to n15
"d5=800u"
#endif

#ifndef PULSE_CHECK
"p17=p7"
#endif

"d11=50m"
"d12=10m"
"d13=25m"
"d22=p2"
"d23=p3"
"d26=p7-p1"
"d27=p7-p23"

;gradient pulses
"p10=3m"      ;at gp0=+50%
"p11=2m"        ;at gp1=-50%
"p12=400u"      ;at gp0=+50%




#define ON
#undef OFF

1	ze
2	1m unblank
	d13 do:N
	d13
	d12
3	d12*3.0
4	10u do:C1
;*****	presaturation	*****
	10u pl7:N
#ifdef ON
#ifdef CARBON_LABEL
	10u fq4:C1	;jump to 56ppm
#endif
	d1
	1m blank
	100u pl1:H
;*****	start 90-degree on h-n	*****
	(p1 ph0)
	d4
	(p7*2 ph6):N (d26 p1*2 ph4)
	d4
;*****	hsqc to nitrogen	*****
	(p1 ph6)
	2u
	10u pl2:f1
	p2 ph18:r
	2u
	p10:gp0     ;GRAD(40, POSITIV, 50)
	5m pl1:f1
	(p17 ph3):N
#ifdef N15T2
	d14
	(p7*2 ph16):N
	d14
#endif
#ifdef N15T1RHO
	2u
	2u pl8:N
	(p8 ph17):N
	2u
	2u pl7:N
#endif
	d0 pl5:C1
#ifdef CARBON_LABEL
	2u
	(p5*2 ph10):C1 (p1*2 ph0):f1
	4u
	2u fq4:C1 pl5:C1	;jump to 177ppm
	(p5*2 ph10):C1
#else
	(p1*2 ph0)
#endif
	d0
	(p7 ph7):N
	2u
	p11:gp1		;GRAD(41, NEGATIV, 40)
	4m
	5u
	(p3:sp1 ph13):f1
	2u
	2u
	50u pl1:f1
	(p1 ph0)
	p12:gp0		;GRAD(50, POSITIV, 8.0)
	d5
	150u pl2:f1
	(p2 ph14:r)
	2u
	5u pl1:f1
	(p1*2 ph15)
	2u
	5u pl2:f1
	(p7*2 ph10):N (p2 ph14:r)
	2u
	p12:gp0		;GRAD(60, POSITIV, 8.0)
	(2u ph0)
	d5
	d8
  5u pl31:N
#endif
#ifndef ONE_D
	go=2 ph31 cpd2:N
	1m unblank
	d13 do:N
	d13 wr #0 if #0 zd
d12 ip3		;nitrogens
lo to 3 times 2
d12 id0
d12 ip31
d12 ip31
lo to 4 times l3
#else
	1m unblank
	lo to 2 times 10
	d1

	10u pl1:f1
	p1 ph31

	(2u ph0)
go=1 ph31
d11 wr #0
#endif

d12 do:C1
d12 do:N
exit

ph0=0
ph1=0
ph3=0 2
ph4=1
ph6=1
ph7=0 0 2 2
ph10=0
ph13=2; adjusted -x
ph14=2
ph15=0

ph16=PHASE_4(0)
     PHASE_4(1)
     PHASE_4(2)
     PHASE_4(3)
ph17=PHASE_4(1)
     PHASE_4(3)
ph18=0;(360)4; adjusted x hl2

#ifdef N15T2
ph31=0 2 2 0 2 0 0 2
#else
ph31=0 2 2 0
#endif
