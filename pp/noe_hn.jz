#ifdef PYTHON_SETUP

T = 298
p = 1
f_h = wf = spec.WATER_FREQUENCY
csw = fcalc.cs_water_Tp( T, p)

filename = write_bruker_search_path('pp', 'noe_hn.jz', sourcefile = 'pp/user/noe_hn.jz')
PUTPARS('PULPROG', filename)

ut.putcomment('SETUP PROTON')
PUTPARS('SFO1', wf)

p1lp = spec.get_pulse_power('H1', 'rect', 6.5, nearby = True)
PUTPARS( ('p1', 'pldb1'), p1lp)

p2lp = spec.get_pulse_power('H1', 'rect', 1000.0*600.0/wf, nearby = True)
PUTPARS(('p2', 'pldb2'), p2lp)

p3lp = spec.get_pulse_power('H1', 'sinc1.0', 1500.0*600.0/wf, nearby = True)
filename = write_bruker_search_path( 'shape', 'sinc1.0', sourcefile = 'wave/user/sinc1.0')
PUTPARS(('p3', 'spdb1', 'spnam1', 'spoffs1'), p3lp + (filename, 0))

p9lp = spec.get_pulse_power('H1', 'rect', 14.4, nearby = False)
PUTPARS(('p9', pldb9'), p9lp)

ut.putcomment('SETUP CARBON')
f_c = fcalc.p2f_water( 177, 'C13', wf, csw )
PUTPARS('SFO3', f_c)

p5lp = spec.get_pulse_power( 'C13', 'rect', 53*600.0/wf)
PUTPARS( ('p5', 'pldb5'), p5lp)

ut.putcomment(' SETUP NITROGEN')
f_n = fcalc.p2f_water( 116.5, 'N15', wf, csw)
PUTPARS('SFO2', f_n)

p7lp = spec.get_pulse_power ( 'N15', 'rect', 40, nearby=True)
PUTPARS( ( 'p7', 'pldb7'), p7lp)

p31lp =spec.get_pulse_power( 'N15', 'rect', 160*600.0/wf, nearby = True)
filename = write_bruker_search_path( 'cpd', 'waltz16.31', sourcefile = 'cpd/user/waltz16.31' )
PUTPARS(('p31','pldb31','CPDPRG2'), p31lp + (filename,))

ut.putcomment('SETUP GRADIENTS')
filename = write_bruker_search_path('gp', 'SINE.20', sourcefile = 'gp/SINE.20')
PUTPARS(('GPNAM0', 'GPX0','GPY0','GPZ0'), (filename, 50, 0, 0))

filename = write_bruker_search_path('gp', 'SINE.20', sourcefile = 'gp/SINE.20')
PUTPARS(('GPNAM1', 'GPX1','GPY1','GPZ1'), (filename, 0, 0, 50))

ut.putcomment('SETUP ACQUISITION')
PUTPARS(('in0', 'in9', 'in10', 'l3'), (0.00002560, 0.00027440, 0.00030000, 120))
PUTPARS( ('dw', 'td','si', '1 td', '1 si'), (50e-6*600.0/wf, 2048, 4096, 480, 1024))

#endif

#include "bits.sg"

;noesy to n
;history
;written by sg 2/23/93
;put in water flip_back 6/1/93
;change to waternh 7/29/93
;changed to noe_hn.sg 8/4/93
;changed to noe_hn.nt 9/14/95
;changed to noe_hn.sg 6/9/96
;changed to noe_hnif.dh 06.04.01


;#define ONE_D
#define INTERLEAVED
#define CARB
#define FLIP_BACK
#define PRESAT_LOOP 205
;#define PRESAT_LOOP 75
#define ON
#undef OFF

;p1    proton 90 at pl1, 6.5u
;p2    1ms proton 90  at pl2 ;sklenar phcor14, phcor21
;p3   ~2ms proton 90 with halfgauss sp1
;p9   = 1.8*p1 @ pl1
"p9=1.8*p1"
"d19=p9"	;dummy delay for nonNOE scans
"l0=1"		;flag to switch between NOE and nonNOE



;	no fq1list needed

;p5=53u	carbon 90 53us pl5

;p7	high power n15 90 on N at pl7
;p31	low power n15 90 (160ms) on N at pl31

"d15=p5*2"

"d5=2.25m-p2-p5*4"
"d9=4u"
"d10=4u"
"d0=d9+14u+p2*2+p1*2+d5+20u+p5*4+d10"

;in0 = d0/(NITROGEN_LOOP+1)
;sw=1/(2in10)
;in9=in10-in0

"d11=50m"
"d12=10m"

;Gradient pulses
"p20=2.5m"		;gp0=+50%
"p21=1.0m"		;gp1=-50%
"p22=0.4m"		;gp0=+50% or gp3=0%(for timing)


1	ze
2	d11
#ifdef INTERLEAVED
	d12
#endif
21	d12
3	d12*5.0
4	5u LOCK_ON
	5u do:N
	5u do:C1
;*****	presaturation	*****
#ifdef ON
	50u pl1:H
	10u pl7:N
	10u pl5:C1

	if "l0 %2 == 1" goto 100

	10u
10	(p9 ph0):H
	15m
	lo to 10 times PRESAT_LOOP
	goto 101

100	10u
110	d19 	;dummy delay for nonNOE
	15m
	lo to 110 times PRESAT_LOOP

101	10u
	1m LOCK_OFF
	50u
	p20:gp0
	5m pl2:H
;*****	start 90-degree on n	*****
	(p7 ph3):N
	d0
	(p7*2 ph17):N
	d9
;*****	proton 180		*****
	(p2 ph21:r):H
	2u
	5u pl1:H
	(p1*2 ph22):H
	2u
	5u pl2:H
	(p2 ph21:r):H
;*****	end proton 180		*****
	d5
;*****	carbon 180		*****
#ifdef CARB
	10u fq4:C1	;to aliph
	(p5*2 ph20):C1
	10u fq4:C1	;to carbonyl
	(p5*2 ph20):C1
#else
	10u
	d15
	10u
	d15
#endif
;*****	end carbon 180		*****
	d10
	(p7 ph7):N
;*****	rev inept to hn		*****
	2u
	p21:gp1
	2m pl3:H
	(p3:sp1 ph13):H
	4u
	50u pl1:H
	(p1 ph0):H
	2u
	p22:gp0
	950u pl2:H
	(p2 ph14:r):H
	2u
	5u pl1:H
	(p1*2 ph15):H
	2u
	5u pl2:H
	(p7*2 ph10):N (p2 ph14:r):H
	2u
	p22:gp0
	(2u ph0)
	950u pl31:N
#endif
#ifndef ONE_D
go=2 ph31 cpds2:N
d11 do:N wr #0 if #0 zd
#ifdef INTERLEAVED
d12 iu0
lo to 21 times 2
#endif
d12 ip3         ;nitrogens
lo to 3 times 2
d12 dd0
d12 id9
d12 id10
d12 ip31
d12 ip31
lo to 4 times l3
#else
	1m LOCK_ON
;	lo to 2 times 20
	10u pl3
	10u
11	(p4*1.5 ph0)
	15m
	lo to 11 times PRESAT_LOOP
	10u
	10u pl1
	p1 ph31
	(2u ph0)
go=1 ph31
d11 wr #0
#endif

d12 do:C1
d12 do:N
1m LOCK_ON
exit

ph0=0
ph1=0
ph3=1 3
ph4=1
ph7=0
ph10=0
ph13=(360) 180; adjusted -x sp0
ph14=2 2 0 0 ; adjusted -x pl2 phcor14
ph15=0 0 2 2
ph17=0 0 0 0 1 1 1 1
     2 2 2 2 3 3 3 3
ph20=0
ph21=PHASE_16( 2 )
     PHASE_16( 0 ) ; adjusted -x pl2 phcor21
ph22= PHASE_16( 0 )
      PHASE_16( 2 )
ph23=0
ph24=1
ph31=0 2 0 2 2 0 2 0
