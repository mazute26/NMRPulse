#ifdef PYTHON_SETUP
ut.putcomment('hello world, here pulse program', 2)
#pp.name_tag = '.myname'


T = 298
p = 1
f_h = wf = spec.WATER_FREQUENCY
csw=fcalc.cs_water_Tp(T, p)

#filename = write_bruker_search_path('pp', 'hnnh3.sg', sourcefile = 'pp/user/hnnh3.sg')
#PUTPARS('PULPROG', filename)


ut.putcomment('SETUP PROTON')
PUTPARS('SFO1', wf)

p1lp = spec.get_pulse_power('H1', 'rect', 7, nearby=True)
PUTPARS(('p1','pldb1'), p1lp)

p2lp = spec.get_pulse_power('H1', 'rect', 1000, nearby=True)
PUTPARS(('p2','pldb2'), p2lp)

p13lp = spec.get_pulse_power('H1', 'sinc1.0', 1000, nearby=True)
filename = write_bruker_search_path('shape', 'sinc1.0', sourcefile = 'wave/user/sinc1.0')
#filename = write_bruker_search_path('shape', 'sinc1.01', sourcefile = 'wave/user/sinc1.0')
PUTPARS(('p13','spdb0','spnam0','spoffs0'), p13lp + (filename, 0))

p30lp = spec.get_pulse_power('H1', 'rect', 50)
filename = write_bruker_search_path('cpd', 'dipsi2.30', sourcefile = 'cpd/user/dipsi2.30')
PUTPARS(('p30','pldb30', 'CPDPRG1'), p30lp + (filename,))

ut.putcomment('SETUP CARBON')
f_cacb = fcalc.p2f_water(46, 'C13', wf, csw)
f_ca = fcalc.p2f_water(56, 'C13', wf, csw)
f_c = fcalc.p2f_water(177, 'C13', wf, csw)
PUTPARS('SFO2', f_c)


flist = [f_c, f_cacb, f_c]
flist_string = ut.flist_to_Bruker_flist(flist)
filename = write_bruker_search_path('f1', 'hnnh.sg', sourcetext = flist_string)
PUTPARS('FQ3LIST', filename)

df_c_ca=(f_c-f_ca)*1e6
df_c_cacb=(f_c-f_cacb)*1e6

p4 = 1e6/df_c_ca*sqrt(0.75)
p4lp = spec.get_pulse_power('C13', 'rect', p4)
filename = write_bruker_search_path('shape', 'rect512', sourcefile = 'wave/user/rect512')
PUTPARS(('p4','spdb4', 'spnam4','spoffs4'), p4lp + (filename, -df_c_ca))

p5 = 1e6/df_c_ca*sqrt(1.-1./16)
p5lp = spec.get_pulse_power('C13', 'rect', p5)
PUTPARS(('p5','pldb5'), p5lp)

filename = write_bruker_search_path('shape', 'rect512', sourcefile = 'wave/user/rect512')
PUTPARS(('p6','spdb6','spnam6','spoffs6'), (p5*2, p5lp[1],filename, df_c_cacb))

p8 = 1e6/df_c_cacb*sqrt(1.-1./16)
p8lp = spec.get_pulse_power('C13', 'rect', p8)
PUTPARS(('p8','pldb8'), p8lp)

p9 = 1e6/df_c_cacb*sqrt(0.75)/2
p9lp = spec.get_pulse_power('C13', 'rect', p9)
PUTPARS(('p9','pldb9'), p9lp)

ut.putcomment('SETUP NITROGEN')
f_n = fcalc.p2f_water(116.5, 'N15', wf, csw)
PUTPARS('SFO3', f_n)

p7 = 40
p7lp = spec.get_pulse_power('N15', 'rect', p7, nearby=True)
PUTPARS(('p7','pldb7'), p7lp)

p31 = 200
p31lp = spec.get_pulse_power('N15', 'rect', p31, nearby=True)
filename = write_bruker_search_path('cpd', 'waltz16.31', sourcefile = 'cpd/user/waltz16.31')
PUTPARS(('p31','pldb31','CPDPRG3'), p31lp + (filename,))


ut.putcomment('SETUP DEUTERIUM')
f_d = fcalc.p2f_water(csw, 'H2', wf, csw)
PUTPARS('SFO4', f_d)

p29 = 150
p29lp = spec.get_pulse_power('H2', 'rect', p29, nearby=True)
filename = write_bruker_search_path('cpd', 'dipsi2.29', sourcefile = 'cpd/user/dipsi2.29')
PUTPARS(('p29','pldb29','CPDPRG4'), p29lp + (filename,))

ut.putcomment('SETUP GRADIENTS')

filename = write_bruker_search_path('gp', 'SINE.20', sourcefile = 'gp/SINE.20')
PUTPARS(('GPNAM1','GPX1','GPY1','GPZ1'), (filename, 0, 0, -50))

ut.putcomment('SETUP ACQUISITION')
ut.putcomment('PROTON1')
PUTPARS(('in3', 'l3'), (500e-6, 25))

ut.putcomment('NITROGEN1')
PUTPARS(('in21', 'l4'), (600e-6, 22))

ut.putcomment('NITROGEN2')
PUTPARS(('in50', 'l5'), (600e-6, 22))

ut.putcomment('PROTON2')
PUTPARS(('DW', 'TD', 'SI', '1 TD', '1 SI'), (54, 1024, 2048, 44, 64))

#endif
;end test

;fq2
;fq5
;ivc
;gp2
#include "bits.sg"

;#define PROTON1
;#define NITROGEN1
#define NITROGEN2

#define ON


;history
;written on 10/23/91
;used for sg hncoct/8 10/23/91
;changed to hnco_fb.sg 9/1/94 included flip-back
;changed to hnnh.sg 8/14/15

;f1 = hydrogen frequency on water
;p1:            proton 90 at pl1
;p2:            proton 90 1ms at pl2 for watergate and first water flip-back pulse
;p13:           proton 90 sinc1.0 sp0 for second water flip-back pulse
;p30:           dipsi 90 at pl30 ca. 50u (dipsi-2.30)


;f2 = carbon frequency at carbonyl resonance (177 ppm)
;"p4=47.4u"	;ca 180 sp4 rect512, as shaped pulse centered at carbon alpha (56 ppm)
                ;with off-resonance offset 177 ppm - 56 ppm
                ;produces n*360 degree pulse on carbon alpha
;"p5=53u"        ;carbonyl 90 at pl5     (63.7u at 500MHz)
                ;produces 360 degree pulse on carbon alpha
;"p6=p5*2"	;carbonyl 180 sp6 rect512 used from 46 ppm to irradiate 177ppm
;"p8=49u"	;cacb 90 at pldb8
;"p9=22u"	;cacb 180 =2*p9 at pldb9

;f3 = nitrogen frequency
;p7=nitrogen 90 at pl7:N
;p31=nitrogen 90 at pl31 for waltz16 decoupling (waltz16.31)




;d1=1.0s        ;relaxation delay
;d7 ca 100u = dw
"d11=50m"
"d12=7m"        ;universal dd,ip delay

;INEPT C'yNz->C'yCA_z
"d13=16.5m"	;half N->C' transfer time
"d15=4.5m"	;half C'->CA transfer time
"d14=d13-d15-p7*2"

;INEPT C'zCA_y-> CA_yNz
"d17=13.5m"	;half CA->N transfer time
"d19=d15"	;half C'->CA transfer time
"d18=d17-d19-p7*2"

"d6=5.4m"              ;refocus HN-N for NH

;PROTON1
"d5=2.25m"	;initial HN transfer
"d4=2u"
"d3=d5-d4-p7*2"

;in3=1/(2sw) : has to be defined

"in5=d5/(l3+1)"
"in4=in3-in5"


;NITROGEN1
"d22=13.6m"	;half N->CO transfer
"d21=6u"
"d23=d22+d21+p4"
"d24=p4+d22-d6-6u-p1"


;in21=in23=1/(2sw) set in21
"in23=in21"
;l4=d23/in23

;NITROGEN2
"d50=13.6m"	;half transfer time
"d52=6u"
"d51=d50-p5*2-d52"
"d54=d50-d52-8u-p1-d6"

;in50=in52=1/(2sw) set in50
"in52=in50"
;l4=d50/in52


"d25=p5-p7" ;p5 > p7
"d26=p7-p1"
"d27=p7-p4/2"


;-------------- Gradient pulses -----------------
;you have to set them by hand because of those $$@! heads
"p20=1.0m"              ;gp1=-50%
"p21=1.0m"              ;gp1=-50%
"p22=0.4m"              ;gp1=-50%
"p23=1.5m"              ;gp1=-50%
"p24=2.3m"		;gp1
"p25=200u"		;gp1

"acqt0=0"


        ze
  10m LOCKDEC_ON
  10m LOCKH_ON
  10m H2_PULSE

2       d11  do:N
#ifdef PROTON1
        d12*2
3       d12*4
4       d12*3
#endif
#ifdef NITROGEN1
        d12
5       d12*3
6       d12*2
#endif
#ifdef NITROGEN2
        d12
7       d12*3
8       d12*2
#endif

        10u
;*******        KILLER-PULSES           *******
        10m H2_LOCK
	10m LOCKH_OFF
        d1 pl7:N
        10u pl5:C1
	10u pl29:D
        10u pl1:H
	10u fq3:C1; jump to 177ppm
        50u LOCKH_ON
 	50u UNBLKGRAMP
	50u H2_PULSE
        (p7 ph10):N (p5 ph20):C1
        p20:gp1         ;GRAD(33, POSITIV, 20)
        2m
#ifdef ON
;*******        start 90-degree         *******
        (p1 ph5):H
        d3
        (p7*2 ph2):N
	d4
        (p1*2 ph1):H
        d5
        (p1 ph2)
        2u
        50u pl2:H
;        (p2 ph24:r)     ;water flip-back using soft pulse
        2u
        p20:gp1         ;GRAD(10, POSITIV, 20)
        2m pl1:H
        (p7 ph3):N
;*******        first n15 evolution, INEPT N-> C'           *******
        (d6 p1 ph26 2u 4u pl30 d24 cpds1 d21):H (d21 p4:sp4 ph20 d22):C1      ;p4 on ca
;        (2.7m d21 p1*2 ph0):H (d21 p4:sp4 ph20 d22):C1      ;p4 on ca for trying water flip-back via radiation damping
        10u pl5:C1
        (d25 p7*2 ph10):N (p5*2 ph20):C1                                            ;p5*2 on co
        10u
        d23 pl5:C1
        (p7 ph10):N
        4u do:H        ;dipsi interrupted (because of p23 gradient)
        4u pl1:H
        (p1 ph27)       ;1h magnetization to z-axis
        p23:gp1
        1.5m
;*******        INEPT C'yNz->C'yCA_z            *******
        (p5 ph4):C1
	5u
	(p4:sp4 ph20):C1
	d13
	5u pl5:C1
	(p5*2 ph2):C1
	5u
	(p7*2 ph10):N
	d14
	(p4:sp4 ph20):C1
	d15
	5u pl5:C1
        (p5 ph20):C1
	3u
	10u fq3:C1; jump to 46ppm
	p24:gp1
	3m
	5u pl8:C1
        (p1 ph26):H    ;1h magnetization back to x-axis
        4u pl30:H
        10u cpds1:H    ;dipsi continued
	10u cpds4:D
;*******        INEPT C'zCA_y-> CA_yNz	   *******
	(p8 ph20):C1
	5u
	(p6:sp6 ph20):C1
	d17
	5u pl9:C1
	(p9*2 ph20):C1
	5u
	(p7*2 ph10):N
	d18
	(p6:sp6 ph20):C1
	d19
	5u pl8:C1
	(p8 ph20):C1
	5u
	5u fq3:C1; jump to 177ppm
        2u do:D
;*******       second n15 evolution           *******
        (p7 ph8):N
	d50
        10u
        (p7*2 ph10):N (d27 p4:sp4 ph20):C1                                            ;p5*2 on co
        5u
	5u pl5:C1
        (d52 d54 4u do 4u pl1 p1 ph27 d6):H (d51 p5*2 ph20 d52):C1      	;p4 on ca
;****************************************************
        (p7 ph0):N
        5u
        p21:gp1         ;GRAD(40, POSITIV, 30)
        3m
        5u
        (p13:sp0 ph25):H       ;water flip-back using sinc1.0 pulse
        2u
        50u pl1:H
        (p1 ph0)
;*******        watergate               *******
        10u
        p22:gp1         ;GRAD(50, POSITIV, 8.0)
        950u pl2:H
	76u			;delay for adjusting first order phase to zero
        2u
        (p2 ph24:r)             ;watergate soft pulse
        2u
        5u pl1:H
        (p1*2 ph0)
        2u
        5u pl2:H
        (p7*2 ph10):N (p2 ph24:r)       ;watergate soft pulse
        2u
        p22:gp1         ;GRAD(51, POSITIV, 8.0)
        10u pl31:N BLKGRAMP
#endif
        950u
go=2 ph31 cpds3:N
5u do:N
d11 wr #0 if #0 zd

#ifdef PROTON1
d12 ip5
d12 ip5
lo to 3 times 2
d12 id3
d12 id4
d12 dd5
d12 ip31*2
lo to 4 times l3
d12 rd3
d12 rd4
d12 rd5
#endif
#ifdef NITROGEN1
d12 ip3
lo to 5 times 2
d12 id21
d12 dd23
d12 ip31*2
lo to 6 times l4
d12 rd21
d12 rd23
#endif
#ifdef NITROGEN2
d12 ip8
lo to 7 times 2
d12 dd50
d12 id52
d12 ip31*2
lo to 8 times l5
d12 rd50
d12 rd52
#endif

1m do:N
1m H2_LOCK
1m LOCKH_OFF
1m LOCKDEC_OFF
exit

ph0=0
ph1=1
ph2=1
ph3=0 0 2 2
ph4=0 2
ph5=(8) 1
ph8=0
ph10=0
ph20=0
ph24=2  ;adjusted -x at pl2
ph25= 2        ;adjusted -x at sp0 phcor25 doesn't work
ph26=(360)90    ;adjusted y for pl1 relativ to pl30
ph27=(360)270   ;adjusted -y for pl1 relativ to pl30
ph31=0 2 2 0
